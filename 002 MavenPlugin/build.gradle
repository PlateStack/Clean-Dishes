buildscript {
    ext.kotlin_version = '1.1.2-4'

    repositories {
        jcenter()
    }

    dependencies {
        //noinspection DifferentKotlinGradleVersion
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

subprojects {
    buildDir = buildDir.parentFile.toPath().resolve("target").resolve("gradle").toFile()

    apply plugin: projectDir.name.toLowerCase()
    if(projectDir.name == 'Scala') {
        ext.scala_version = '2.12.2'

        configurations {
            scalaCompiler
        }

        dependencies {
            scalaCompiler "org.scala-lang:scala-compiler:$scala_version"
            scalaCompiler "org.scala-lang:scala-library:$scala_version"
            scalaCompiler "org.scala-lang:scala-reflect:$scala_version"
        }

        tasks.withType(ScalaCompile) {
            scalaClasspath = configurations.scalaCompiler
        }
    }

    repositories {
        jcenter()
        maven { url 'http://dl.bintray.com/kotlin/kotlinx' }
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        compile 'com.github.PlateStack:PlateAPI:a50fd1d41e'
    }
}
